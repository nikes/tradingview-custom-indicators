//@version=3
// Step 1. Script settings
study(title="Double Exponential Moving Average", shorttitle="DEMA", overlay=true)

// Input options
srcData = input(title="Source Data", type=source, defval=close)

// Length settings
len1 = input(title="Length DEMA #1", type=integer, defval=21, minval=1)
len2 = input(title="Length DEMA #2", type=integer, defval=0, minval=0)
len3 = input(title="Length DEMA #3", type=integer, defval=0, minval=0)

// Visual options
showSignals = input(title="Show Signals?", type=bool, defval=true)

// Step 2. Calculate indicator values
// Function that calculates the DEMA
DEMA(series, length) =>
    if (length > 0)
        emaValue = ema(series, length)
        2 * emaValue - ema(emaValue, length)
    else
        na

// Calculate the DEMA values
demaVal1 = DEMA(srcData, len1)
demaVal2 = DEMA(srcData, len2)
demaVal3 = DEMA(srcData, len3)

// Step 3. Determine indicator signals
// See if there's a DEMA crossover
demaCrossover = if (len2 > 0) and (len3 > 0)
    crossover(demaVal1, demaVal2) and (demaVal3 > demaVal3[1])
else
    if (len2 > 0) and (len3 == 0)
        crossover(demaVal1, demaVal2)
    else
        if (len3 > 0) and (len2 == 0)
            crossover(demaVal1, demaVal3)
        else
            crossover(close, demaVal1)

// Check if there's a DEMA crossunder
demaCrossunder = if (len2 > 0) and (len3 > 0)
    crossunder(demaVal1, demaVal2) and (demaVal3 < demaVal3[1])
else
    if (len2 > 0) and (len3 == 0)
        crossunder(demaVal1, demaVal2)
    else
        if (len3 > 0) and (len2 == 0)
            crossunder(demaVal1, demaVal3)
        else
            crossunder(close, demaVal1)

// Step 4. Output indicator data
// Plot DEMAs on the chart
plot(series=demaVal1, color=orange, linewidth=2, title="DEMA #1")
plot(series=demaVal2, color=blue, linewidth=2, title="DEMA #2")
plot(series=demaVal3, color=fuchsia, linewidth=2, title="DEMA #3")

// Highlight DEMA signals
bgColour = demaCrossover ? green :
     demaCrossunder ? red :
     na

bgcolor(color=showSignals ? bgColour : na)

// Step 5. Create indicator alerts
alertcondition(condition=demaCrossover,
     title="Crossover",
     message="DEMA generated a buy (crossover) signal")

alertcondition(condition=demaCrossunder,
     title="Crossunder",
     message="DEMA generated a sell (crossunder) signal")
